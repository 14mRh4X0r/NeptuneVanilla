buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'minecrell-snapshots'
            url = 'http://repo.minecrell.net/snapshots'
        }
    }
    dependencies {
        classpath 'net.minecrell:VanillaGradle:1.0.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}
apply plugin: 'net.minecrell.vanilla'
ext.minecraftPlugin = plugins.getPlugin('net.minecrell.vanilla')
apply from: project(':Common').file('gradle/implementation.gradle')

minecraft {
    serverTweaker = 'org.neptunepowered.vanilla.launch.server.NeptuneServerTweaker'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.neptunepowered.vanilla.launch.server.NeptuneServerMain',
                'Class-Path': 'minecraft_server.1.8.jar lib/launchwrapper-1.11.jar'
        )
    }
}

configurations {
    // While we could set this to 'runtime', Eclipse doesn't support runtime dependencies
    // properly so this was always going before 'compile' in Eclipse
    shadow
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.0.4'

    shadow 'org.ow2.asm:asm-all:5.0.4'
}

shadowJar {
    configurations += [project.configurations.shadow]

    from minecraftPlugin.delayedFile('{SRG_DIR}/notch-srg.srg')
    rename 'notch-srg.srg', 'mappings.srg'

    dependencies {
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')
    }
}
assemble.dependsOn shadowJar
